// WF fitnessScaling.slim : ^Total fixed mutations: (.*)$
//
// Test using fitnessScaling to modify fitness values
//
initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 1e5-1);
	initializeRecombinationRate(1e-7);
}
1 early() {
	sim.addSubpop("p1", 1000);
}
1: late() {
	// promote individuals carrying mutations
	// without this, the run total is usually ~150; with, ~1000
	inds = p1.individuals;
	inds.fitnessScaling = 0.0;
	
	s = sample(inds, 5);
	s.fitnessScaling = 0.1;
	
	for (ind in s)
		if (sum(ind.genomes.countOfMutationsOfType(m1)) > 0)
			ind.fitnessScaling = 1.0;
}
20000 late() { cat("\nTotal fixed mutations: " + sim.substitutions.size() + "\n"); }
